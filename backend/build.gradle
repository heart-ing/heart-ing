plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.10'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id "org.sonarqube" version "3.4.0.2513"
    id 'jacoco'
}

group = 'com.chillin'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.google.code.gson:gson:2.10.1'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    // annotationProcessor
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.2'

    //s3
    implementation "com.amazonaws:aws-java-sdk-s3:1.12.281"

    // Data JPA Test
    testImplementation 'com.h2database:h2'

    //  help make parsing to json easier
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // twitter
    implementation group: 'org.springframework.social', name: 'spring-social-twitter', version: '1.1.2.RELEASE'

    // mockito static
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.5.1'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

}

tasks.named('test') {
    useJUnitPlatform()
}

def    jacocoExcludes = [
        '**/config/*',
        '**/exception/*',
        '**/request/*',
        '**/response/*',
        '**/data/*',
        '**/oauth/*',
        '**/jwt/*',
        '**/Hearting*',
        '**/OAuth*',
        '**/Migration*',
]

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
    jacoco {
        excludes = jacocoExcludes
    }

}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
    afterEvaluate {
        //dto 및 외부 연동 서비스는 테스트에서 제외
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)})
        )
    }
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = layout.buildDirectory.dir('reports/jacoco')
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            // 'element'가 없으면 프로젝트의 전체 파일을 합친 값을 기준으로 합니다.
            element = 'CLASS'
            excludes = [
                    '**.config.*',
                    '**.exception.*',
                    '**.request.*',
                    '**.response.*',
                    '**.data.*',
                    '**.oauth.*',
                    '**.jwt.*',
                    '**.Hearting*',
                    '**.OAuth*',
                    '**.Migration*',
            ]

            limit {
                // 'counter'를 지정하지 않으면 default는 'INSTRUCTION'
                // 'value'를 지정하지 않으면 default는 'COVEREDRATIO'
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
}

task testCoverage(type: Test) {
    group 'verification'
    description 'Runs the unit tests with coverage'

    dependsOn(':test',
            ':jacocoTestReport',
            ':jacocoTestCoverageVerification')

    tasks['jacocoTestReport'].mustRunAfter(tasks['test'])
    tasks['jacocoTestCoverageVerification'].mustRunAfter(tasks['jacocoTestReport'])
}